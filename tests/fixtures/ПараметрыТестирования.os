Перем ПараметрыКластера;
Перем ПараметрыТестирования;
Перем ВозвращаемыеЗначения;
Перем Лог;

// Процедура устанавливает вывод функции ВыполнитьКоманду мок-исполнителя команд
//
// Параметры:
//    ИсполнительКоманд    - МокИсполнительКоманд        - мок-объект исполнитель команд
//    ПутьКДанным            - Строка                    - Путь к параметрам в структуре параметров кластера
//    
Процедура УстановитьВыводИсполнителяКоманд(ИсполнительКоманд, ПутьКДанным) Экспорт

	Если ТипЗнч(ИсполнительКоманд) = Тип("ИсполнительКоманд") Тогда
		Возврат;
	КонецЕсли;

	МассивПутей = СтрРазделить(ПутьКДанным, ".");

	Данные = ПараметрыКластера;

	Для Каждого ТекПуть Из МассивПутей Цикл
		Данные = Данные[ТекПуть];
	КонецЦикла;

	ДанныеДляОбработки = Новый Массив();
	Если ТипЗнч(Данные) = Тип("Массив") Тогда
		ДанныеДляОбработки = Данные;
	Иначе
		ДанныеДляОбработки.Добавить(Данные);
	КонецЕсли;

	Для Каждого ТекДанные Из ДанныеДляОбработки Цикл
		ИсполнительКоманд.Когда().ВыполнитьКоманду(ТекДанные.ПараметрыКоманды).ТогдаВозвращает(ТекДанные.ВыводКоманды);
	КонецЦикла;

	ИсполнительКоманд.Когда().КодВозврата().ТогдаВозвращает(0);

КонецПроцедуры // УстановитьВыводИсполнителяКомандНов()

Функция Агент_СтрокаПодключения()

	Возврат Параметры().Агент_Адрес + ":" + Параметры().Агент_Порт;

КонецФункции // Агент_СтрокаПодключения()

Функция Кластер_Ид()

	Кластеры = РазобратьВыводКоманды(ВозвращаемыеЗначения["Кластеры"]);

	Возврат Кластеры[0]["cluster"];

КонецФункции // Кластер_Ид()

Функция Агент_СтрокаАвторизации()

	Если НЕ Параметры().Свойство("Агент_Администратор") Тогда
		Возврат "";
	КонецЕсли;

	Если ПустаяСтрока(Параметры().Агент_Администратор) Тогда
		Возврат "";
	КонецЕсли;

	СтрокаАвторизации = СтрШаблон("--agent-user=%1", Служебный.ОбернутьВКавычки(Параметры().Агент_Администратор));
	
	Если Не ПустаяСтрока(Параметры().Агент_Пароль) Тогда
		СтрокаАвторизации = СтрокаАвторизации + СтрШаблон(" --agent-pwd=%1", Параметры().Агент_Пароль);
	КонецЕсли;

	Возврат СтрокаАвторизации;

КонецФункции // Кластер_СтрокаАвторизации()

Функция Кластер_СтрокаАвторизации()

	Если НЕ Параметры().Свойство("Кластер_Администратор") Тогда
		Возврат "";
	КонецЕсли;

	Если ПустаяСтрока(Параметры().Кластер_Администратор) Тогда
		Возврат "";
	КонецЕсли;

	СтрокаАвторизации = СтрШаблон("--cluster-user=%1", Служебный.ОбернутьВКавычки(Параметры().Кластер_Администратор));
	
	Если Не ПустаяСтрока(Параметры().Кластер_Пароль) Тогда
		СтрокаАвторизации = СтрокаАвторизации + СтрШаблон(" --cluster-pwd=%1", Параметры().Кластер_Пароль);
	КонецЕсли;

	Возврат СтрокаАвторизации;

КонецФункции // Кластер_СтрокаАвторизации()

Функция Сервер_Ид()

	Серверы = РазобратьВыводКоманды(ВозвращаемыеЗначения["Серверы"]);

	Возврат Серверы[0]["server"];

КонецФункции // Сервер_Ид()

Функция ПрофильБезопасности_Имя()

	Профиль = РазобратьВыводКоманды(ВозвращаемыеЗначения["ПрофилиБезопасности"]);

	Возврат Профиль[0]["name"];

КонецФункции // ПрофильБезопасности_Имя()

Функция ИБ_СтрокаАвторизации()

	Если НЕ Параметры().Свойство("ИБ_Администратор") Тогда
		Возврат "";
	КонецЕсли;

	Если ПустаяСтрока(Параметры().ИБ_Администратор) Тогда
		Возврат "";
	КонецЕсли;

	СтрокаАвторизации = СтрШаблон("--infobase-user=%1", Служебный.ОбернутьВКавычки(Параметры().ИБ_Администратор));
	
	Если Не ПустаяСтрока(Параметры().ИБ_Пароль) Тогда
		СтрокаАвторизации = СтрокаАвторизации + СтрШаблон(" --infobase-pwd=%1", Параметры().ИБ_Пароль);
	КонецЕсли;

	Возврат СтрокаАвторизации;

КонецФункции // ИБ_СтрокаАвторизации()

Функция Вывод_АдминистраторыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента", Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииАгента", Агент_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("agent.admin", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Администраторы"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_АдминистраторыСписок()

Функция Вывод_АдминистраторыСписокПослеДобавления()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента", Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииАгента", Агент_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("agent.admin", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Администраторы"] + ВозвращаемыеЗначения["Администраторы.Добавление"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_АдминистраторыСписокПослеДобавления()

Функция Вывод_КластерыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("cluster", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Кластеры"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_КластерыСписок()

Функция Вывод_КластерыПараметры()
	
	Результат = Новый Массив();

	Кластеры = РазобратьВыводКоманды(ВозвращаемыеЗначения["Кластеры"]);

	Для Каждого ТекКластер Из Кластеры Цикл

		ПараметрыКоманды = Новый Соответствие();
		ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"   , Агент_СтрокаПодключения());
		ПараметрыКоманды.Вставить("ИдентификаторКластера"     , ТекКластер["cluster"]);
	
		ПараметрыОбъекта = Новый КомандыОбъекта("cluster", ПараметрыКоманды);
	        
		ВыводКоманды = ТекКластер["ТекстОбъекта"];

		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
	                                       ПараметрыОбъекта.ПараметрыКоманды("Описание"),
	                                       ВыводКоманды));

	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_КластерыПараметры()

Функция Вывод_КластерыАдминистраторыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииАгента"  , Агент_СтрокаАвторизации());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("cluster.admin", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Кластеры.Администраторы"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_КластерыАдминистраторыСписок()

Функция Вывод_КластерыАдминистраторыСписокПослеДобавления()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииАгента"  , Агент_СтрокаАвторизации());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("cluster.admin", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Кластеры.Администраторы"]
	             + ВозвращаемыеЗначения["Кластеры.Администраторы.Добавление"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_КластерыАдминистраторыСписокПослеДобавления()

Функция Вывод_МенеджерыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("manager", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Менеджеры"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_МенеджерыСписок()

Функция Вывод_СерверыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("server", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Серверы"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_СерверыСписок()

Функция Вывод_СерверыПараметры()
	
	Результат = Новый Массив();

	Серверы = РазобратьВыводКоманды(ВозвращаемыеЗначения["Серверы"]);

	Для Каждого ТекСервер Из Серверы Цикл

		ПараметрыКоманды = Новый Соответствие();
		ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
		ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
		ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
		ПараметрыКоманды.Вставить("ИдентификаторСервера"     , ТекСервер["server"]);
	    
		ПараметрыОбъекта = Новый КомандыОбъекта("server", ПараметрыКоманды);

		ВыводКоманды = ТекСервер["ТекстОбъекта"];

		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
	                                       ПараметрыОбъекта.ПараметрыКоманды("Описание"),
	                                       ВыводКоманды));

	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_СерверыПараметры()

Функция Вывод_СерверыСписокПослеДобавления()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("server", ПараметрыКоманды);

	ВыводКоманды = "";

	ВыводКоманды = ВозвращаемыеЗначения["Серверы"] +
	               ВозвращаемыеЗначения["Серверы.Добавление"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_СерверыСписокПослеДобавления()

Функция Вывод_СерверыДобавить()
	
	Серверы = РазобратьВыводКоманды(ВозвращаемыеЗначения["Серверы.Добавление"]);

	Имя         = Серверы[0].Получить("name");
	АдресАгента = Серверы[0].Получить("agent-host");
	ПортАгента  = Серверы[0].Получить("agent-port");

	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыКоманды.Вставить("Имя"                      , Имя);
	ПараметрыКоманды.Вставить("АдресАгента"              , АдресАгента);
	ПараметрыКоманды.Вставить("ПортАгента"               , ПортАгента);

	ПараметрыКоманды.Вставить("ДиапазонПортов"                       , Серверы[0].Получить("port-range"));
	ПараметрыКоманды.Вставить("ЦентральныйСервер"                    , Серверы[0].Получить("using"));
	ПараметрыКоманды.Вставить("МенеджерПодКаждыйСервис"              , Серверы[0].Получить("dedicate-managers"));
	ПараметрыКоманды.Вставить("КоличествоИБНаПроцесс"                , Серверы[0].Получить("infobases-limit"));
	ПараметрыКоманды.Вставить("МаксОбъемПамятиРабочихПроцессов"      , Серверы[0].Получить("memory-limit"));
	ПараметрыКоманды.Вставить("КоличествоСоединенийНаПроцесс"        , Серверы[0].Получить("connections-limit"));
	ПараметрыКоманды.Вставить("ПортГлавногоМенеджераКластера"        , Серверы[0].Получить("cluster-port"));
	ПараметрыКоманды.Вставить("БезопасныйОбъемПамятиРабочихПроцессов",
	                          Серверы[0].Получить("safe-working-processes-memory-limit"));
	ПараметрыКоманды.Вставить("БезопасныйРасходПамятиЗаОдинВызов"    , Серверы[0].Получить("safe-call-memory-limit"));

	ПараметрыОбъекта = Новый КомандыОбъекта("server", ПараметрыКоманды);

	ВремТекст = Новый ТекстовыйДокумент();
	ВремТекст.УстановитьТекст(ВозвращаемыеЗначения["Серверы.Добавление"]);
	ВыводКоманды = ВремТекст.ПолучитьСтроку(1);

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Добавить"), ВыводКоманды);

КонецФункции // Вывод_ИБДобавить()

Функция Вывод_РабочиеПроцессыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("process", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["РабочиеПроцессы"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_РабочиеПроцессыСписок()

Функция Вывод_РабочиеПроцессыПараметры()
	
	Результат = Новый Массив();

	Процессы = РазобратьВыводКоманды(ВозвращаемыеЗначения["РабочиеПроцессы"]);

	Для Каждого ТекПроцесс Из Процессы Цикл

		ПараметрыКоманды = Новый Соответствие();
		ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
		ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
		ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
		ПараметрыКоманды.Вставить("ИдентификаторПроцесса"    , ТекПроцесс["process"]);
	    
		ПараметрыОбъекта = Новый КомандыОбъекта("process", ПараметрыКоманды);

		ВыводКоманды = ТекПроцесс["ТекстОбъекта"];

		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
	                                       ПараметрыОбъекта.ПараметрыКоманды("Описание"),
	                                       ВыводКоманды));

	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_РабочиеПроцессыПараметры()

Функция Вывод_РабочиеПроцессыСписокЛицензии()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("process.license", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["РабочиеПроцессы.Лицензии"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_РабочиеПроцессыСписокЛицензии()

Функция Вывод_РабочиеПроцессыПараметрыЛицензии()
	
	Результат = Новый Массив();

	ПроцессыЛицензии = РазобратьВыводКоманды(ВозвращаемыеЗначения["РабочиеПроцессы.Лицензии"]);

	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());

	Для Каждого ТекПроцесс Из ПроцессыЛицензии Цикл

		ПараметрыКоманды.Вставить("ИдентификаторПроцесса"    , ТекПроцесс["process"]);
	
		ПараметрыОбъекта = Новый КомандыОбъекта("process.license", ПараметрыКоманды);

		ВыводКоманды = ТекПроцесс["ТекстОбъекта"];
		
		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
										   ПараметрыОбъекта.ПараметрыКоманды("Описание"),
										   ВыводКоманды));
	
	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_РабочиеПроцессыПараметрыЛицензии()

Функция СокращенныйТекстОписанияИБ(ТекстОписания)

	КоличествоПроверяемыхСимволовИмени = 4;

	ВыводКоманды = "";

	Текст = Новый ТекстовыйДокумент();
	Текст.УстановитьТекст(ТекстОписания);

	Для й = 1 По Текст.КоличествоСтрок() Цикл
		ТекстСтроки = Текст.ПолучитьСтроку(й);
		ИмяПоля = Лев(ТекстСтроки, КоличествоПроверяемыхСимволовИмени);

		Если НЕ (ИмяПоля = "name" ИЛИ ИмяПоля = "info" ИЛИ ИмяПоля = "desc") Тогда
			Продолжить;
		КонецЕсли;

		ВыводКоманды = ВыводКоманды + ТекстСтроки + Символы.ПС;
	КонецЦикла;

	Возврат ВыводКоманды + Символы.ПС;

КонецФункции // СокращенныйТекстОписанияИБ()

Функция Вывод_ИБСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("infobase", ПараметрыКоманды);

	ВыводКоманды = "";

	ИБ = РазобратьВыводКоманды(ВозвращаемыеЗначения["ИнформационныеБазы"]);

	Для Каждого ТекИб Из ИБ Цикл

		ВыводКоманды = ВыводКоманды + СокращенныйТекстОписанияИБ(ТекИБ["ТекстОбъекта"]);
	    
	КонецЦикла;

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_ИБСписок()

Функция Вывод_ИБСокращенныеПараметры()
	
	Результат = Новый Массив();

	ИБ = РазобратьВыводКоманды(ВозвращаемыеЗначения["ИнформационныеБазы"]);

	Для Каждого ТекИб Из ИБ Цикл

		ПараметрыКоманды = Новый Соответствие();
		ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
		ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
		ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
		ПараметрыКоманды.Вставить("ИдентификаторИБ"          , ТекИБ["infobase"]);
	
		ПараметрыОбъекта = Новый КомандыОбъекта("infobase", ПараметрыКоманды);
	        
		ВыводКоманды = СокращенныйТекстОписанияИБ(ТекИБ["ТекстОбъекта"]);

		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
	                                       ПараметрыОбъекта.ПараметрыКоманды("Описание"),
	                                       ВыводКоманды));

	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_ИБСокращенныеПараметры()

Функция Вывод_ИБПолныеПараметры()
	
	Результат = Новый Массив();

	ИБ = РазобратьВыводКоманды(ВозвращаемыеЗначения["ИнформационныеБазы"]);

	Для Каждого ТекИб Из ИБ Цикл

		ПараметрыКоманды = Новый Соответствие();
		ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
		ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
		ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
		ПараметрыКоманды.Вставить("ИдентификаторИБ"          , ТекИБ["infobase"]);
		ПараметрыКоманды.Вставить("СтрокаАвторизацииИБ"      , ИБ_СтрокаАвторизации());
	
		ПараметрыОбъекта = Новый КомандыОбъекта("infobase", ПараметрыКоманды);
	        
		ВыводКоманды = ТекИБ["ТекстОбъекта"];

		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
	                                       ПараметрыОбъекта.ПараметрыКоманды("ПолноеОписание"),
	                                       ВыводКоманды));

	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_ИБПолныеПараметры()

Функция Вывод_ИБНедостаточноПрав()
	
	Результат = Новый Массив();

	ИБ = РазобратьВыводКоманды(ВозвращаемыеЗначения["ИнформационныеБазы"]);

	Для Каждого ТекИб Из ИБ Цикл

		ПараметрыКоманды = Новый Соответствие();
		ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
		ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
		ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
		ПараметрыКоманды.Вставить("ИдентификаторИБ"          , ТекИБ["infobase"]);
		ПараметрыКоманды.Вставить("СтрокаАвторизацииИБ"      , "");
	
		ПараметрыОбъекта = Новый КомандыОбъекта("infobase", ПараметрыКоманды);
	        
		ВыводКоманды = СтрШаблон("Недостаточно прав пользователя для доступа к базе %1", ТекИБ["name"]);
	
		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
	                                       ПараметрыОбъекта.ПараметрыКоманды("ПолноеОписание"),
	                                       ВыводКоманды));

	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_ИБНедостаточноПрав()

Функция Вывод_ИБСписокПослеДобавления()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("infobase", ПараметрыКоманды);

	ВыводКоманды = "";

	ИБ = РазобратьВыводКоманды(ВозвращаемыеЗначения["ИнформационныеБазы"] +
	                           ВозвращаемыеЗначения["ИнформационныеБазы.Добавление"]);

	Для Каждого ТекИб Из ИБ Цикл

		ВыводКоманды = ВыводКоманды + СокращенныйТекстОписанияИБ(ТекИБ["ТекстОбъекта"]);
	    
	КонецЦикла;

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_ИБСписокПослеДобавления()

Функция Вывод_ИБДобавить()
	
	Имя             = Параметры().НоваяИБ_Имя;
	СоздатьБазуСУБД = Истина;
	Локализация     = "ru_RU";

	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыКоманды.Вставить("Имя"            , Имя);
	ПараметрыКоманды.Вставить("Локализация"    , Локализация);
	ПараметрыКоманды.Вставить("СоздатьБазуСУБД", СоздатьБазуСУБД);

	ПараметрыКоманды.Вставить("ТипСУБД"                              , Перечисления.ТипыСУБД.MSSQLServer);
	ПараметрыКоманды.Вставить("АдресСервераСУБД"                     , "localhost");
	ПараметрыКоманды.Вставить("ИмяБазыСУБД"                          , Имя);
	ПараметрыКоманды.Вставить("ИмяПользователяБазыСУБД"              , "_1CSrvUsr1");
	ПараметрыКоманды.Вставить("ПарольПользователяБазыСУБД"           , "q2w3e4r5");
	ПараметрыКоманды.Вставить("БлокировкаРегламентныхЗаданийВключена", Перечисления.СостоянияВыключателя.Выключено);
	ПараметрыКоманды.Вставить("ВыдачаЛицензийСервером"               , Перечисления.ПраваДоступа.Разрешено);

	ПараметрыОбъекта = Новый КомандыОбъекта("infobase", ПараметрыКоманды);

	ВремТекст = Новый ТекстовыйДокумент();
	ВремТекст.УстановитьТекст(ВозвращаемыеЗначения["ИнформационныеБазы.Добавление"]);
	ВыводКоманды = ВремТекст.ПолучитьСтроку(1);

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Добавить"), ВыводКоманды);

КонецФункции // Вывод_ИБДобавить()

Функция Вывод_СеансыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("session", ПараметрыКоманды);

	ВыводКоманды = "";

	Сеансы = РазобратьВыводКоманды(ВозвращаемыеЗначения["Сеансы"]);

	Для Каждого ТекСеанс Из Сеансы Цикл

		ВыводКоманды = ВыводКоманды + ТекСеанс["ТекстОбъекта"];
	    
	КонецЦикла;

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_СеансыСписок()

Функция Вывод_СеансыПараметры()
	
	Результат = Новый Массив();

	Сеансы = РазобратьВыводКоманды(ВозвращаемыеЗначения["Сеансы"]);

	Для Каждого ТекСеанс Из Сеансы Цикл

		ПараметрыКоманды = Новый Соответствие();
		ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
		ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
		ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
		ПараметрыКоманды.Вставить("ИдентификаторСеанса"      , ТекСеанс["session"]);
	
		ПараметрыОбъекта = Новый КомандыОбъекта("session", ПараметрыКоманды);
	        
		ВыводКоманды = ТекСеанс["ТекстОбъекта"];

		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
	                                       ПараметрыОбъекта.ПараметрыКоманды("Описание"),
	                                       ВыводКоманды));

	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_СеансыПараметры()

Функция Вывод_СеансыСписокЛицензии()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("session.license", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["Сеансы.Лицензии"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_СеансыСписокЛицензии()

Функция Вывод_СеансыПараметрыЛицензии()
	
	Результат = Новый Массив();

	СеансыЛицензии = РазобратьВыводКоманды(ВозвращаемыеЗначения["Сеансы.Лицензии"]);

	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());

	Для Каждого ТекСеанс Из СеансыЛицензии Цикл

		ПараметрыКоманды.Вставить("ИдентификаторСеанса"      , ТекСеанс["session"]);
	
		ПараметрыОбъекта = Новый КомандыОбъекта("session.license", ПараметрыКоманды);

		ВыводКоманды = ТекСеанс["ТекстОбъекта"];
		
		Результат.Добавить(Новый Структура("ПараметрыКоманды, ВыводКоманды",
										   ПараметрыОбъекта.ПараметрыКоманды("Описание"),
										   ВыводКоманды));
	
	КонецЦикла;

	Возврат Результат;

КонецФункции // Вывод_СеансыПараметрыЛицензии()

Функция Вывод_СоединенияСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("connection");
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["Соединения"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_СоединенияСписок()

Функция Вывод_БлокировкиСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("lock", ПараметрыКоманды);

	ВыводКоманды = "";

	Блокировки = РазобратьВыводКоманды(ВозвращаемыеЗначения["Блокировки"]);

	Для Каждого ТекБлокировка Из Блокировки Цикл

		ВыводКоманды = ВыводКоманды + ТекБлокировка["ТекстОбъекта"];
	    
	КонецЦикла;

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);


КонецФункции // Вывод_БлокировкиСписок()

Функция Вывод_СервисыСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("service", ПараметрыКоманды);

	ВыводКоманды = "";

	Сервисы = РазобратьВыводКоманды(ВозвращаемыеЗначения["Сервисы"]);

	Для Каждого ТекСервис Из Сервисы Цикл

		ВыводКоманды = ВыводКоманды + ТекСервис["ТекстОбъекта"];
	    
	КонецЦикла;

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_СервисыСписок()

Функция Вывод_НазначенияФункциональностиСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("rule");
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--server=%1", Сервер_Ид()));
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["НазначенияФункциональности"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_НазначенияФункциональностиСписок()

Функция Вывод_НазначенияФункциональностиПараметры()
	
	Назначения = РазобратьВыводКоманды(ВозвращаемыеЗначения["НазначенияФункциональности"]);

	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("rule");
	ПараметрыЗапуска.Добавить("info");

	ПараметрыЗапуска.Добавить(СтрШаблон("--server=%1", Сервер_Ид()));
	
	ПараметрыЗапуска.Добавить(СтрШаблон("--rule=%1", Назначения[0]["rule"]));

	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = Назначения[0]["ТекстОбъекта"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_НазначенияФункциональностиПараметры()

Функция Вывод_ПрофилиБезопасностиСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());
	
	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("list");
	
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());
	
	ВыводКоманды = ВозвращаемыеЗначения["ПрофилиБезопасности"];
	
	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиСписок()

Функция Вывод_ПрофилиБезопасностиПараметры()
	
	Профили = РазобратьВыводКоманды(ВозвращаемыеЗначения["ПрофилиБезопасности"]);

	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());
	
	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("info");
	
	ПараметрыЗапуска.Добавить(СтрШаблон("--name=%1", ПрофильБезопасности_Имя()));

	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());
	
	ВыводКоманды = Профили[0]["ТекстОбъекта"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиСписок()

Функция Вывод_ПрофилиБезопасностиКаталогиСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("acl");
	ПараметрыЗапуска.Добавить(Перечисления.ВидыОбъектовПрофиляБезопасности.Каталог);
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--name=%1", ПрофильБезопасности_Имя()));
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["ПрофилиБезопасности.Каталоги"];
	
	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиКаталогиСписок()

Функция Вывод_ПрофилиБезопасностиCOMКлассыСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("acl");
	ПараметрыЗапуска.Добавить(Перечисления.ВидыОбъектовПрофиляБезопасности.COMКласс);
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--name=%1", ПрофильБезопасности_Имя()));
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["ПрофилиБезопасности.COMКлассы"];
	
	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиCOMКлассыСписок()

Функция Вывод_ПрофилиБезопасностиКомпонентыСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("acl");
	ПараметрыЗапуска.Добавить(Перечисления.ВидыОбъектовПрофиляБезопасности.Компонент);
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--name=%1", ПрофильБезопасности_Имя()));
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["ПрофилиБезопасности.Компоненты"];
	
	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиКаталогиСписок()

Функция Вывод_ПрофилиБезопасностиМодулиСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("acl");
	ПараметрыЗапуска.Добавить(Перечисления.ВидыОбъектовПрофиляБезопасности.Модуль);
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--name=%1", ПрофильБезопасности_Имя()));
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["ПрофилиБезопасности.Модули"];
	
	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиМодулиСписок()

Функция Вывод_ПрофилиБезопасностиПриложенияСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("acl");
	ПараметрыЗапуска.Добавить(Перечисления.ВидыОбъектовПрофиляБезопасности.Приложение);
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--name=%1", ПрофильБезопасности_Имя()));
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["ПрофилиБезопасности.Приложения"];
	
	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиПриложенияСписок()

Функция Вывод_ПрофилиБезопасностиИнтернетРесурсыСписок()
	
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("profile");
	ПараметрыЗапуска.Добавить("acl");
	ПараметрыЗапуска.Добавить(Перечисления.ВидыОбъектовПрофиляБезопасности.ИнтернетРесурс);
	ПараметрыЗапуска.Добавить("list");

	ПараметрыЗапуска.Добавить(СтрШаблон("--name=%1", ПрофильБезопасности_Имя()));
	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ВыводКоманды = ВозвращаемыеЗначения["ПрофилиБезопасности.ИнтернетРесурсы"];
	
	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_ПрофилиБезопасностиИнтернетРесурсыСписок()

Функция Вывод_СчетчикиПотребленияРесурсовСписок()
	
	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"  , Агент_СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"    , Кластер_Ид());
	ПараметрыКоманды.Вставить("СтрокаАвторизацииКластера", Кластер_СтрокаАвторизации());
	
	ПараметрыОбъекта = Новый КомандыОбъекта("counter", ПараметрыКоманды);

	ВыводКоманды = ВозвращаемыеЗначения["СчетчикиПотребленияРесурсов"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыОбъекта.ПараметрыКоманды("Список"), ВыводКоманды);

КонецФункции // Вывод_СчетчикиПотребленияРесурсовСписок()


Функция Вывод_СчетчикиПотребленияРесурсовЗначения()
	
	Счетчики = РазобратьВыводКоманды(ВозвращаемыеЗначения["СчетчикиПотребленияРесурсов"]);

	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Агент_СтрокаПодключения());

	ПараметрыЗапуска.Добавить("counter");
	
	ПараметрыЗапуска.Добавить(СтрШаблон("--counter=%1", Счетчики[0]["name"]));

	ПараметрыЗапуска.Добавить(СтрШаблон("--cluster=%1", Кластер_Ид()));
	ПараметрыЗапуска.Добавить(Кластер_СтрокаАвторизации());

	ПараметрыЗапуска.Добавить("values");

	ВыводКоманды = ВозвращаемыеЗначения["СчетчикиПотребленияРесурсов.Значения"];

	Возврат Новый Структура("ПараметрыКоманды, ВыводКоманды", ПараметрыЗапуска, ВыводКоманды);

КонецФункции // Вывод_СчетчикиПотребленияРесурсовЗначения()

Процедура Инициализация()
	
	Лог = Логирование.ПолучитьЛог("ktb.lib.irac");

	КаталогФикстур = ТекущийСценарий().Каталог;

	ВозвращаемыеЗначения = ПрочитатьМакетыОбъектовСервера(КаталогФикстур);
	
	ПараметрыКластера = Новый Структура();

	ПараметрыКластера.Вставить("Администраторы",
	    Новый Структура("Список, СписокПослеДобавления, Параметры, Добавить, Изменить, Удалить"));
	ПараметрыКластера.Администраторы.Список                = Вывод_АдминистраторыСписок();
	ПараметрыКластера.Администраторы.СписокПослеДобавления = Вывод_АдминистраторыСписокПослеДобавления();

	ПараметрыКластера.Вставить("Кластеры",
	    Новый Структура("Список, Параметры, Администраторы, Добавить, Изменить, Удалить"));
	ПараметрыКластера.Кластеры.Список            = Вывод_КластерыСписок();
	ПараметрыКластера.Кластеры.Параметры         = Вывод_КластерыПараметры();
	ПараметрыКластера.Кластеры.Администраторы    = Новый Структура("Список,
	                                                              |СписокПослеДобавления,
	                                                              |Параметры,
	                                                              |Добавить,
	                                                              |Изменить,
	                                                              |Удалить");
	ПараметрыКластера.Кластеры.Администраторы.Список                = Вывод_КластерыАдминистраторыСписок();
	ПараметрыКластера.Кластеры.Администраторы.СписокПослеДобавления = Вывод_КластерыАдминистраторыСписокПослеДобавления();

	ПараметрыКластера.Вставить("Менеджеры",
		Новый Структура("Список, Параметры"));
	ПараметрыКластера.Менеджеры.Список        = Вывод_МенеджерыСписок();

	ПараметрыКластера.Вставить("Серверы",
	    Новый Структура("Список, СписокПослеДобавления, Параметры, Добавить, Изменить, Удалить"));
	ПараметрыКластера.Серверы.Список                = Вывод_СерверыСписок();
	ПараметрыКластера.Серверы.СписокПослеДобавления = Вывод_СерверыСписокПослеДобавления();
	ПараметрыКластера.Серверы.Добавить              = Вывод_СерверыДобавить();
	ПараметрыКластера.Серверы.Параметры             = Вывод_СерверыПараметры();

	ПараметрыКластера.Вставить("РабочиеПроцессы",
	    Новый Структура("Список, Параметры, Лицензии"));
	ПараметрыКластера.РабочиеПроцессы.Список     = Вывод_РабочиеПроцессыСписок();
	ПараметрыКластера.РабочиеПроцессы.Параметры  = Вывод_РабочиеПроцессыПараметры();
	ПараметрыКластера.РабочиеПроцессы.Лицензии   = Новый Структура("Список, Описание");
	ПараметрыКластера.РабочиеПроцессы.Лицензии.Список   = Вывод_РабочиеПроцессыСписокЛицензии();
	ПараметрыКластера.РабочиеПроцессы.Лицензии.Описание = Вывод_РабочиеПроцессыПараметрыЛицензии();

	ПараметрыКластера.Вставить("Сервисы",
	    Новый Структура("Список, Параметры"));
	ПараметрыКластера.Сервисы.Список    = Вывод_СервисыСписок();

	ПараметрыКластера.Вставить("Сеансы",
	    Новый Структура("Список, Параметры, Лицензии"));
	ПараметрыКластера.Сеансы.Список        = Вывод_СеансыСписок();
	ПараметрыКластера.Сеансы.Параметры     = Вывод_СеансыПараметры();
	ПараметрыКластера.Сеансы.Лицензии      = Новый Структура("Список, Описание");
	ПараметрыКластера.Сеансы.Лицензии.Список   = Вывод_СеансыСписокЛицензии();
	ПараметрыКластера.Сеансы.Лицензии.Описание = Вывод_СеансыПараметрыЛицензии();

	ПараметрыКластера.Вставить("Соединения",
	    Новый Структура("Список, Параметры"));
	ПараметрыКластера.Соединения.Список        = Вывод_СоединенияСписок();

	ПараметрыКластера.Вставить("Блокировки",
	    Новый Структура("Список, Параметры"));
	ПараметрыКластера.Блокировки.Список        = Вывод_БлокировкиСписок();

	ПараметрыКластера.Вставить("ИБ",
	    Новый Структура("Список,
	                    |СписокПослеДобавления,
	                    |СокращенныеПараметры,
	                    |ПолныеПараметры,
	                    |НедостаточноПрав,
	                    |Добавить,
	                    |Изменить,
	                    |Удалить"));
	ПараметрыКластера.ИБ.Список                 = Вывод_ИБСписок();
	ПараметрыКластера.ИБ.СписокПослеДобавления  = Вывод_ИБСписокПослеДобавления();
	ПараметрыКластера.ИБ.Добавить               = Вывод_ИБДобавить();
	ПараметрыКластера.ИБ.СокращенныеПараметры   = Вывод_ИБСокращенныеПараметры();
	ПараметрыКластера.ИБ.ПолныеПараметры        = Вывод_ИБПолныеПараметры();
	ПараметрыКластера.ИБ.НедостаточноПрав       = Вывод_ИБНедостаточноПрав();

	ПараметрыКластера.Вставить("НазначенияФункциональности",
	    Новый Структура("Список, Параметры, Добавить, Изменить, Удалить"));
	ПараметрыКластера.НазначенияФункциональности.Список       = Вывод_НазначенияФункциональностиСписок();
	ПараметрыКластера.НазначенияФункциональности.Параметры    = Вывод_НазначенияФункциональностиПараметры();

	СтруктураПрофилей = Новый Структура("Список,
	                                    |Параметры,
	                                    |Добавить,
	                                    |Изменить,
	                                    |Удалить,
	                                    |Каталоги,
	                                    |COMКлассы,
	                                    |Компоненты,
	                                    |Модули,
	                                    |Приложения,
	                                    |ИнтернетРесурсы");

	СтруктураПрофилей.Список            = Вывод_ПрофилиБезопасностиСписок();
	СтруктураПрофилей.Параметры         = Вывод_ПрофилиБезопасностиПараметры();
	СтруктураПрофилей.Каталоги          = Новый Структура("Список, Параметры",
	                                                      Вывод_ПрофилиБезопасностиКаталогиСписок());
	СтруктураПрофилей.COMКлассы         = Новый Структура("Список, Параметры",
	                                                      Вывод_ПрофилиБезопасностиCOMКлассыСписок());
	СтруктураПрофилей.Компоненты        = Новый Структура("Список, Параметры",
	                                                      Вывод_ПрофилиБезопасностиКомпонентыСписок());
	СтруктураПрофилей.Модули            = Новый Структура("Список, Параметры",
	                                                      Вывод_ПрофилиБезопасностиМодулиСписок());
	СтруктураПрофилей.Приложения        = Новый Структура("Список, Параметры",
	                                                      Вывод_ПрофилиБезопасностиПриложенияСписок());
	СтруктураПрофилей.ИнтернетРесурсы   = Новый Структура("Список, Параметры",
	                                                      Вывод_ПрофилиБезопасностиИнтернетРесурсыСписок());
	
	ПараметрыКластера.Вставить("ПрофилиБезопасности", СтруктураПрофилей);

	ПараметрыКластера.Вставить("СчетчикиПотребленияРесурсов",
	    Новый Структура("Список, Значения"));
	ПараметрыКластера.СчетчикиПотребленияРесурсов.Список    = Вывод_СчетчикиПотребленияРесурсовСписок();
	ПараметрыКластера.СчетчикиПотребленияРесурсов.Значения  = Вывод_СчетчикиПотребленияРесурсовЗначения();

КонецПроцедуры // Инициализация()

Функция ПрочитатьМакетыОбъектовСервера(Знач ПутьККаталогу)

	СтруктураМакетов = Новый Соответствие();

	МассивФайлов = НайтиФайлы(ПутьККаталогу, "Макет_*.txt");

	ДлинаПрефикса = 7;

	Для Каждого ТекМакет Из МассивФайлов Цикл
		ТекстМакета = Новый ТекстовыйДокумент();
		ТекстМакета.Прочитать(ТекМакет.ПолноеИмя, КодировкаТекста.UTF8);

		СтруктураМакетов.Вставить(Сред(ТекМакет.ИмяБезРасширения, ДлинаПрефикса), ТекстМакета.ПолучитьТекст());
	КонецЦикла;

	Возврат СтруктураМакетов;

КонецФункции // ПрочитатьМакетыОбъектовСервера()

// Возвращает текущие параметры тестового окружения
//
// Параметры:
//    Обновить          - Булево         - Истина - обновить параметры тестирования принудительно
//
// Возвращаемое значение:
//    Структура         - параметры тестового окружения
//
Функция Параметры(Обновить = Ложь) Экспорт

	Если ТипЗнч(ПараметрыТестирования) = Тип("Структура") И НЕ Обновить Тогда
		Возврат ПараметрыТестирования;
	КонецЕсли;

	Параметры = Новый Структура();

	ЭтоСерверСборок = Ложь;
	ЭтоСерверСборок = ВРег(ПолучитьПеременнуюСреды("CI")) = ВРег("true");

	Параметры.Вставить("ЭтоСерверСборок"      , ЭтоСерверСборок);

	// Проверим что есть "местный" файл параметров тестирования
	ПутьКФайлуПараметров = ОбъединитьПути(ТекущийСценарий().Каталог, "ПараметрыТестирования.txt");

	МакетПараметров = Новый Файл(ПутьКФайлуПараметров);

	// Если "местного" файла нет, то используем файл с параметрами по умолчанию
	Если НЕ МакетПараметров.Существует() Тогда
		ПутьКФайлуПараметров = ОбъединитьПути(ТекущийСценарий().Каталог, "ПараметрыТестирования.ПоУмолчанию.txt");
	КонецЕсли;

	МакетПараметров = Новый Файл(ПутьКФайлуПараметров);

	Если МакетПараметров.Существует() Тогда

		ПараметрыПоУмолчанию = ПрочитатьПараметрыТестированияИзФайла(ПутьКФайлуПараметров);
	    
		Для Каждого ТекПараметр Из ПараметрыПоУмолчанию Цикл
			Параметры.Вставить(ТекПараметр.Ключ, ТекПараметр.Значение);
		КонецЦикла;

		ПараметрыТестирования = Параметры;

	КонецЕсли;
	
	Возврат ПараметрыТестирования;

КонецФункции // Параметры()

Функция ПрочитатьПараметрыТестированияИзФайла(Знач ПутьКФайлу)

	Параметры = Новый Структура();

	МакетПараметров = Новый Файл(ПутьКФайлу);

	Если НЕ МакетПараметров.Существует() Тогда
		Возврат Параметры;
	КонецЕсли;

	ТекстМакета = Новый ТекстовыйДокумент();
	ТекстМакета.Прочитать(МакетПараметров.ПолноеИмя, КодировкаТекста.UTF8);

	Для й = 1 По ТекстМакета.КоличествоСтрок() Цикл

		МассивПараметр = СтрРазделить(ТекстМакета.ПолучитьСтроку(й), "=");

		Если МассивПараметр.Количество() = 0 Тогда
			Продолжить;
		ИначеЕсли МассивПараметр.Количество() = 1 Тогда
			МассивПараметр.Добавить("");
		КонецЕсли;

		Попытка
			Параметры.Вставить(СокрЛП(МассивПараметр[0]), СокрЛП(МассивПараметр[1]));
		Исключение
			Лог.Отладка("Ошибка чтения параметра """ + СокрЛП(МассивПараметр[0]) + """: " + ОписаниеОшибки());
		КонецПопытки;

	КонецЦикла;

	Возврат Параметры;

КонецФункции // ПрочитатьПараметрыТестированияИзФайла()

// Функция преобразует переданный текст вывода команды в массив соответствий
// элементы массива создаются по блокам текста, разделенным пустой строкой
// пары <ключ, значение> структуры получаются для каждой строки с учетом разделителя ":"
//   
// Параметры:
//   ВыводКоманды            - Строка            - текст для разбора
//   
// Возвращаемое значение:
//    Массив (Соответствие) - результат разбора
//
Функция РазобратьВыводКоманды(Знач ВыводКоманды)
	
	Текст = Новый ТекстовыйДокумент();
	Текст.УстановитьТекст(ВыводКоманды);

	МассивРезультатов = Новый Массив();
	Описание = Новый Соответствие();

	ТекстОбъекта = "";

	Для й = 1 По Текст.КоличествоСтрок() Цикл

		ТекстСтроки = Текст.ПолучитьСтроку(й);
	    
		ПозРазделителя = СтрНайти(ТекстСтроки, ":");

		Если НЕ ЗначениеЗаполнено(ТекстСтроки) Тогда
			Если й = 1 Тогда
				Продолжить;
			КонецЕсли;
			Описание.Вставить("ТекстОбъекта", ТекстОбъекта + Символы.ПС);
			МассивРезультатов.Добавить(Описание);
			Описание = Новый Соответствие();
			ТекстОбъекта = "";
			Продолжить;
		ИначеЕсли ПозРазделителя = 0 Тогда
			Продолжить;
		КонецЕсли;
	    
		Описание.Вставить(СокрЛП(Лев(ТекстСтроки, ПозРазделителя - 1)), СокрЛП(Сред(ТекстСтроки, ПозРазделителя + 1)));
		ТекстОбъекта = ТекстОбъекта + ТекстСтроки + Символы.ПС;

	КонецЦикла;

	Возврат МассивРезультатов;

КонецФункции // РазобратьВыводКоманды()

Инициализация();